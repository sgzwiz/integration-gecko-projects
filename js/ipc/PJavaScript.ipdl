/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 4 -*-
 * vim: set ts=4 sw=4 et tw=80:
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PContent;
include DOMTypes;

using mozilla::void_t;

namespace mozilla {
namespace jsipc {

struct JSIID {
    uint32_t m0;
    uint16_t m1;
    uint16_t m2;
    uint8_t m3_0;
    uint8_t m3_1;
    uint8_t m3_2;
    uint8_t m3_3;
    uint8_t m3_4;
    uint8_t m3_5;
    uint8_t m3_6;
    uint8_t m3_7;
};

union JSVariant {
    void_t;     /* |undefined| */
    nsString;   /* StringValue(x) */
    uint32_t;   /* JSObject */
    double;     /* NumberValue(x) */
    bool;       /* BooleanValue(x) */
    JSIID;      /* XPC nsIID */
};

struct ReturnStatus {
    bool ok;        /* True for success, false for failure. */
    JSVariant exn;  /* Exception, if |ok| is false. */
};

union JSParam {
    void_t;     /* value is strictly an xpc out param */
    JSVariant;  /* actual value to pass through */
};

struct PPropertyDescriptor
{
    uint32_t    objId;
    uint32_t    attrs;
    uint32_t    shortid;
    JSVariant   value;

    // How to interpret these values depends on whether JSPROP_GETTER/SETTER
    // are set. If set, the corresponding value is a CPOW or 0 for NULL.
    // Otherwise, the following table is used:
    //
    //  0 - NULL
    //  1 - Default getter or setter.
    //  2 - js_GetterOnlyPropertyStub (setter only)
    //  3 - Unknown
    uint32_t    getter;
    uint32_t    setter;
};

rpc protocol PJavaScript
{
    manager PContent;

child:
    // The parent process no longer holds any references to the child object.
    async DropObject(uint32_t objId);

    // JSAPI call wrappers.
    urgent HasHook(uint32_t objId, nsString id) returns (ReturnStatus rs, bool has);
    urgent HasOwnHook(uint32_t objId, nsString id) returns (ReturnStatus rs, bool has);
    urgent GetHook(uint32_t objId, uint32_t receiverId, nsString id) returns (ReturnStatus rs, JSVariant result);
    urgent SetHook(uint32_t objId, uint32_t receiverId, nsString id, bool strict) returns (ReturnStatus rs, JSVariant result);
    urgent CallHook(uint32_t objId, JSParam[] argv) returns (ReturnStatus rs, JSVariant result, JSParam[] outparams);

    urgent InstanceOf(uint32_t objId, JSIID iid) returns (ReturnStatus rs, bool instanceof);
    urgent GetPropertyDescriptor(uint32_t objId, nsString id, uint32_t flags) returns (ReturnStatus rs, PPropertyDescriptor result);

parent:
    async __delete__();
};

}
}
