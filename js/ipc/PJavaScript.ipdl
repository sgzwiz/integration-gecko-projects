/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 4 -*-
 * vim: set ts=4 sw=4 et tw=80:
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PContent;
include DOMTypes;

using mozilla::void_t;

namespace mozilla {
namespace jsipc {

union JSVariant {
    void_t;     /* |undefined| */
    nsString;   /* StringValue(x) */
    uint32_t;   /* 0 for null, >0 for object ID */
    double;     /* NumberValue(x) */
    bool;       /* BooleanValue(x) */
};

struct ReturnStatus {
    bool ok;        /* True for success, false for failure. */
    JSVariant exn;  /* Exception, if |ok| is false. */
};

rpc protocol PJavaScript
{
    manager PContent;

child: 
    // The parent process no longer holds any references to the child object.
    async DropObject(uint32_t objId);

    // JSAPI call wrappers.
    rpc AddProperty(uint32_t objId, nsString id) returns (ReturnStatus rs);
    rpc GetProperty(uint32_t objId, nsString id) returns (ReturnStatus rs, JSVariant result);
    rpc SetProperty(uint32_t objId, nsString id, JSVariant val) returns (ReturnStatus rs, JSVariant result);
    rpc DeleteProperty(uint32_t objId, nsString id) returns (ReturnStatus rs, JSVariant result);
    rpc NewResolve(uint32_t objId, nsString id, uint32_t flags) returns (ReturnStatus rs, uint32_t obj2);
    rpc Call(JSVariant[] argv) returns (ReturnStatus rs, JSVariant result);

parent:
    async __delete__();
};

}
}
